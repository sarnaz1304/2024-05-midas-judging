Slow Frost Cottonmouth

high

# Decimals mishandling allows for first deposit to be less than the minimum amount to deposit

## Summary

The protocol has a `minAmountToDepositInEuro` value for the first deposit of each user. Decimals mishandling in the `DepositVault` contract allows users to bypass that value and deposit less.

## Vulnerability Detail

The protocol, as mentioned in the contest [README](https://github.com/sherlock-audit/2024-05-midas/blob/main/README.md?plain=1#L15) file, expects the protocol to only use the `USDC` token as the deposit token, which has 6 decimals. The problem doesn't come from the fact that the `USDC` token only has 6 decimals. The problem would also exist with a stablecoin like `DAI` which has 18 decimals. 

In the `DepositVault` contract the deposit functions expects the `amountUsdIn` parameter to be in 10**18 decimals (as stated by the function NatSpec)

```javascript
    /**
     * @inheritdoc IDepositVault
     * @dev transfers `tokenIn` from `msg.sender`
     * to `tokensReceiver`
     * @param tokenIn address of token to deposit.
@>   * @param amountUsdIn amount of token to deposit in 10**18 decimals.
     */
    function deposit(address tokenIn, uint256 amountUsdIn)
        external
        onlyGreenlisted(msg.sender)
        whenNotPaused
    {
        address user = msg.sender;

        _requireTokenExists(tokenIn);

        lastRequestId.increment();
        uint256 requestId = lastRequestId.current();

        if (!isFreeFromMinDeposit[user]) {
@>          _validateAmountUsdIn(user, amountUsdIn);
        }
        require(amountUsdIn > 0, "DV: invalid amount");

        totalDeposited[user] += amountUsdIn;
        _tokenTransferFromUser(tokenIn, amountUsdIn);

        emit Deposit(requestId, user, tokenIn, amountUsdIn);
    }
```

If the user is not `isFreeFromMinDeposit` which means that the deposit amount must be checked to be over the `minAmountToDepositInEuro`, then the `amountUsdIn` is validated in the `_validateAmountUsdIn` function:

```javascript
    /**
     * @dev validates that inputted USD amount >= minAmountToDepositInUsd()
     * @param user user address
     * @param amountUsdIn amount of USD
     */
    function _validateAmountUsdIn(address user, uint256 amountUsdIn)
        internal
        view
    {
        if (totalDeposited[user] != 0) return;
        require(
@>          amountUsdIn >= minAmountToDepositInUsd(),
            "DV: usd amount < min"
        );
    }
```

In this function inside the `require` statement, there is a comparison been made between `amountUsdIn` which has 18 decimal places and `minAmountToDepositInUsd()` which has no decimal places. This is the reason `amountUsdIn` will always be bigger than `minAmountToDepositInUsd()` and the `require` statement will falsely succeed (unless very very small amount of `USDC` gets deposited).

Let's see the the `minAmountToDepositInUsd()` to confirm that it has no decimals:

```javascript
    /**
     * @notice minAmountToDepositInEuro converted to USD in base18
     */
    function minAmountToDepositInUsd() public view returns (uint256) {
        return
@>          (minAmountToDepositInEuro * eurUsdDataFeed.getDataInBase18()) /
            10**18;
    }
```
First of all, it is stated in the [protocol docs](https://docs.midas.app/additional-topics/faq) under the last question `What is the minimum investment required to mint mTBILL?` that the minimum investment is 100,000 euros. Also, this number is frequently used in the test suite as the `minAmountToDepositInEuro`. This number doesn't have any decimal places, in the tests it is always set as `100_000`. 

It is then multiplied by `eurUsdDataFeed.getDataInBase18())` which returns a number with 18 decimals, taken from a EUR/USD `Chainlink aggregator` as 8 decimals and then converted to 18 decimals through the `convertToBase18` function (shown in the code below). 

**So we have `minAmountToDepositInEuro` with 0 decimals multiplied to `eurUsdDataFeed.getDataInBase18())` with 18 decimals and then divided by `10**18`, which makes the result have 0 decimals.**

```javascript
feeds/DataFeed.sol

    function getDataInBase18() external view returns (uint256 answer) {
@>      (, answer) = _getDataInBase18();
    }

    function _getDataInBase18()
        private
        view
        returns (uint80 roundId, uint256 answer)
    {
        uint8 decimals = aggregator.decimals();
        (uint80 _roundId, int256 _answer, , uint256 updatedAt, ) = aggregator
            .latestRoundData();
        require(_answer > 0, "DF: feed is deprecated");
        require(
            // solhint-disable-next-line not-rely-on-time
            block.timestamp - updatedAt <= _HEALTHY_DIFF,
            "DF: feed is unhealthy"
        );
        roundId = _roundId;
@>      answer = uint256(_answer).convertToBase18(decimals);
    }

libraries/DecimalsCorrectionLibrary.sol

    function convertToBase18(uint256 originalAmount, uint256 originalDecimals) internal pure returns (uint256) {
@>      return convert(originalAmount, originalDecimals, 18);
    }

    function convert(uint256 originalAmount, uint256 originalDecimals, uint256 decidedDecimals)
        internal
        pure
        returns (uint256)
    {
        if (originalAmount == 0) return 0;
        if (originalDecimals == decidedDecimals) return originalAmount;

        uint256 adjustedAmount;

        if (originalDecimals > decidedDecimals) {
            adjustedAmount = originalAmount / (10 ** (originalDecimals - decidedDecimals));
        } else {
            adjustedAmount = originalAmount * (10 ** (decidedDecimals - originalDecimals));
        }

        return adjustedAmount;
    }
```

In conclusion, the reason that the user can deposit less than `minAmountToDepositInEuro` is because there is a comparison made between a value with 18 decimals and a value with 0 decimals, as shown above.

## Impact

A user can deposit significantly smaller amounts than the `minAmountToDepositInEuro` value and pass the checks with no problems.

## Proof of Code

In the following test `DAI` is actually used instead of `USDC`. This is because the protocol team also used `DAI` in most of their tests and for sake of time (the auditing timeline is small) I have edited and used one of their tests to prove my point. As proven above, the issue is not because of the token decimals (`DAI`'s 18 vs `USDC`'s 6), rather than a bad comparison. I will happily provide a `USDC` test if needed though.

Paste the following code in the `test/DepositVault.test.ts` file (after the deployment functions):

```javascript
  describe(`Proof of Code`, () => {
    it('does not fail when depositing less than the minimum deposit amount', async () => {
      const {
        depositVault,
        accessControl,
        mockedAggregator,
        mockedAggregatorEur,
        owner,
        mTBILL,
        stableCoins,
      } = await loadFixture(defaultDeploy);
      await addPaymentTokenTest(
        { vault: depositVault, owner },
        stableCoins.dai,
      );
      await setRoundData({ mockedAggregator }, 4);

      // Mint 1 dai to the owner, 18 decimals
      await mintToken(stableCoins.dai, owner, 1_000_000_000_000_000_000);

      // This approve function actually gets the `amount` and converts it to 18 decimals
      // So by passing 1 we approve 1 whole dai token
      await approveBase18(owner, stableCoins.dai, depositVault, 1);

      await greenList(
        { accessControl, greenlistable: depositVault, owner },
        owner,
      );
      await setRoundData({ mockedAggregator: mockedAggregatorEur }, 1);

      // This is Min Amount to Deposit in Euros, so no need for decimal places.
      await setMinAmountToDepositTest({ depositVault, owner }, 100_000);

      // We try to deposit 1 dai, so 18 decimals and the test doesn't fail, meaning it's possible
      await deposit({ depositVault, owner, mTBILL }, stableCoins.dai, 1_000_000_000_000_000_000);
    });
  });
```

## Code Snippet

[https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L91](https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L91)
[https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L155](https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L155l)
[https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L137](https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L137)
[https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/feeds/DataFeed.sol#L64](https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/feeds/DataFeed.sol#L64)
[https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/libraries/DecimalsCorrectionLibrary.sol#L63](https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/libraries/DecimalsCorrectionLibrary.sol#L63)

## Tool used

Manual Review

## Recommendation

Since `amountUsdIn` has 18 decimals, the simplest solution would be to compare it with a value with 18 decimals. You can do that not dividing by 10**18 in the `minAmountToDepositInUsd()` which converts EUR to USD:

```diff
    /**
     * @notice minAmountToDepositInEuro converted to USD in base18
     */
    function minAmountToDepositInUsd() public view returns (uint256) {
-       return (minAmountToDepositInEuro * eurUsdDataFeed.getDataInBase18()) / 10**18;
+       return (minAmountToDepositInEuro * eurUsdDataFeed.getDataInBase18());
    }
```