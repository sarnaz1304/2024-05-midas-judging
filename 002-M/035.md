Dazzling Midnight Caterpillar

medium

# Incorrect rounding down - Users can increase their totalDeposits by transfering zero amount.

## Summary
During a deposit to a deposit vault contract, the contract first increments the `totalDeposited` storage variable by `amountUsdIn` (18 decimal places) and then transfers the converted amount in token decimals from the user.
The `_tokenTransferFromUser` function in the `ManageableVault` contract calculates the converted amount using rounding down, so `tokensReceiver` may receive less than the amount incremented from `totalDeposited`.

## Vulnerability Detail
During a deposit vault contract, the contract increases 'totalDeposited' by the input amount ('amountUsdIn') instead of the transfer amount.
```solidity
function deposit(address tokenIn, uint256 amountUsdIn)
        external
        onlyGreenlisted(msg.sender)
        whenNotPaused
    {
        address user = msg.sender;

        _requireTokenExists(tokenIn);

        lastRequestId.increment();
        uint256 requestId = lastRequestId.current();

        if (!isFreeFromMinDeposit[user]) {
            _validateAmountUsdIn(user, amountUsdIn);
        }
        require(amountUsdIn > 0, "DV: invalid amount");

@>      totalDeposited[user] += amountUsdIn;
@>      _tokenTransferFromUser(tokenIn, amountUsdIn); 

        emit Deposit(requestId, user, tokenIn, amountUsdIn);
    }
```
The `_tokenTransferFromUser` function in the `ManageableVault` contract calculates the converted amount using rounding down, so `tokensReceiver` may receive less than the amount incremented from `totalDeposited`.
```solidity
function _tokenTransferFromUser(address token, uint256 amount) internal {
        IERC20(token).safeTransferFrom(
            msg.sender,
            tokensReceiver,
            amount.convertFromBase18(_tokenDecimals(token))
        );
}

// DecimalsCorrectionLibrary.sol - library
function convert(
        uint256 originalAmount,
        uint256 originalDecimals,
        uint256 decidedDecimals
    ) internal pure returns (uint256) {
        if (originalAmount == 0) return 0;
        if (originalDecimals == decidedDecimals) return originalAmount;

        uint256 adjustedAmount;

        if (originalDecimals > decidedDecimals) {
@>          adjustedAmount =
                originalAmount /
                (10**(originalDecimals - decidedDecimals));
        } else {
            adjustedAmount =
                originalAmount *
                (10**(decidedDecimals - originalDecimals));
        }

        return adjustedAmount;
    }
```
As we can see in `convert` function of  `DecimalsCorrectionLibrary` library, if originalDecimals > decidedDecimals, then adjustedAmount is rounded down.
Supported USDC token has 6 decimals, so less amount may be transferred to the protocol than increased amount in `totalDeposited` in result.
If user sets `amountUsdIn` to 10 * 12 - 1 in `deposit` function, `totalDeposited` increased but user just transfers zero amount to the vault. 
Malicious actors can perform this action repeatedly (or batched in one transaction using malicious contract), and increase their deposited amount repeatedly by consuming only gas.  

## Impact
Malicious actors can increase their deposited amount without sending any tokens to receive mTBILL tokens.

## Code Snippet
https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L91-L112
https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/abstract/ManageableVault.sol#L151-L157
https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/libraries/DecimalsCorrectionLibrary.sol#L18-L39

## Tool used

Manual Review

## Recommendation
Calculate the increase in `tokensReceiver` balance before and after token transfer and increase `totalDeposited` according to the difference.
