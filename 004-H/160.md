Loud Turquoise Yak

high

# User Can Self-Revoke Role

## Summary

A user can self-revoke a role assigned by the admin, including the `BLACKLISTED_ROLE ` role.

## Vulnerability Detail

The project uses the OpenZeppelin library version 4.9.x for permission management. The main contract responsible for permissions is `WithMidasAccessControl`, which is inherited by `Greenlistable` and `Blacklistable`. These contracts are initialized with an instance of the `MidasAccessControl` contract, which inherits from `AccessControlUpgradeable` in the OpenZeppelin library. Roles are managed by the admin.

`WithMidasAccessControl -> Greenlistable/Blacklistable -> MidasAccessControl -> AccessControlUpgradeable`

The issue lies in the `renounceRole` function of the `AccessControlUpgradeable` contract, which allows the sender to remove any assigned role from themselves.

```solidity
File: midas-contracts/node_modules/@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol
170:     /**
171:      * @dev Revokes `role` from the calling account.
172:      *
173:      * Roles are often managed via {grantRole} and {revokeRole}: this function's
174:      * purpose is to provide a mechanism for accounts to lose their privileges
175:      * if they are compromised (such as when a trusted device is misplaced).
176:      *
177:      * If the calling account had been revoked `role`, emits a {RoleRevoked}
178:      * event.
179:      *
180:      * Requirements:
181:      *
182:      * - the caller must be `account`.
183:      *
184:      * May emit a {RoleRevoked} event.
185:      */
186:     function renounceRole(bytes32 role, address account) public virtual override {              //@audit public access
187:         require(account == _msgSender(), "AccessControl: can only renounce roles for self");
188: 
189:         _revokeRole(role, account);
190:     }

File: midas-contracts/node_modules/@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol
241:     /**
242:      * @dev Revokes `role` from `account`.
243:      *
244:      * Internal function without access restriction.
245:      *
246:      * May emit a {RoleRevoked} event.
247:      */
248:     function _revokeRole(bytes32 role, address account) internal virtual {
249:         if (hasRole(role, account)) {
250:             _roles[role].members[account] = false;
251:             emit RoleRevoked(role, account, _msgSender());
252:         }
253:     }
```

## Impact

Any user can call the `renounceRole` function in the `MidasAccessControl` contract to remove any role assigned by the admin. While removing the `GREENLISTED_ROLE` role will only prevent the user from making deposits and redemptions, removing the `BLACKLISTED_ROLE` role will allow the user to transfer `mTBILL` tokens, which the admin has explicitly restricted.

## Code Snippet

```solidity
File: midas-contracts/contracts/mTBILL.sol
086:     /**
087:      * @dev overrides _beforeTokenTransfer function to ban
088:      * blacklisted users from using the token functions
089:      */
090:     function _beforeTokenTransfer(
091:         address from,
092:         address to,
093:         uint256 amount
094:     )
095:         internal
096:         virtual
097:         override(ERC20PausableUpgradeable)
098:         onlyNotBlacklisted(from)             //@audit
099:         onlyNotBlacklisted(to)				  //@audit
100:     {
101:         ERC20PausableUpgradeable._beforeTokenTransfer(from, to, amount);
102:     }
```

https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/mTBILL.sol#L98-L99


https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/DepositVault.sol#L93

https://github.com/sherlock-audit/2024-05-midas/blob/main/midas-contracts/contracts/RedemptionVault.sol#L63

### PoC

```js
    it.only('User Can Self-Revoke BLACKLISTED_ROLE', async () => {
      const { accessControl, blackListableTester, owner, regularAccounts } =
        await loadFixture(defaultDeploy);
      await blackList(
        { blacklistable: blackListableTester, accessControl, owner },
        regularAccounts[0],
      );
      console.log(
        'Is blacklisted',
        await accessControl.hasRole(
          await blackListableTester.BLACKLISTED_ROLE(),
          regularAccounts[0].address,
        ),
      );

      await accessControl
        .connect(regularAccounts[0])
        .renounceRole(
          await blackListableTester.BLACKLISTED_ROLE(),
          regularAccounts[0].address,
        );

      console.log(
        'Is blacklisted',
        await accessControl.hasRole(
          await blackListableTester.BLACKLISTED_ROLE(),
          regularAccounts[0].address,
        ),
      );
    });
  });
```

```shell
➜  midas-contracts git:(main) ✗ yarn test --no-compile
yarn run v1.22.21
$ hardhat test --no-compile
{ FORKING_NETWORK: undefined }


  Blacklistable
    addToBlackList
Is blacklisted true
Is blacklisted false
      ✔ User Can Self-Revoke BLACKLISTED_ROLE (7223ms)


  1 passing (7s)

✨  Done in 19.13s.
```


## Tool used

Manual Review

## Recommendation

Users should be prevented from renouncing assigned roles, either entirely or for specific roles.

Example implementation for the `BLACKLISTED_ROLE` role:

```solidity
function renounceRole(bytes32 role, address account) public override {
    require(role != BLACKLISTED_ROLE, "Cannot renounce blacklisted role");
    super.renounceRole(role, account);
}
```